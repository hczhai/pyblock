CMAKE_MINIMUM_REQUIRED(VERSION 3.0)

FIND_PROGRAM(CMAKE_C_COMPILER NAMES $ENV{CC} gcc PATHS ENV PATH NO_DEFAULT_PATH)
FIND_PROGRAM(CMAKE_CXX_COMPILER NAMES $ENV{CXX} g++ PATHS ENV PATH NO_DEFAULT_PATH)

# Project Name (must be python module name)

SET(PROJECT_NAME block)
PROJECT(${PROJECT_NAME} VERSION 1.5.3)

# Check Python3 and Pybind11

SET(Python_ADDITIONAL_VERSIONS 3)
FIND_PACKAGE(PythonInterp)

IF (NOT PYTHONINTERP_FOUND)
    MESSAGE(FATAL_ERROR "Python3 not found.")
ENDIF()

FIND_PACKAGE(PythonLibs)

EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE}-config --extension-suffix
    OUTPUT_VARIABLE PYLIB_SUFFIX OUTPUT_STRIP_TRAILING_WHITESPACE)
EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE} -c "import pybind11;print(pybind11.get_include())"
    OUTPUT_VARIABLE PYBIND_INCLUDE_DIRS OUTPUT_STRIP_TRAILING_WHITESPACE)

MESSAGE(STATUS "PROJECT_NAME = ${PROJECT_NAME}")
MESSAGE(STATUS "PYTHON_VERSION_MAJOR = ${PYTHON_VERSION_MAJOR}")
MESSAGE(STATUS "PYTHON_VERSION_MINOR = ${PYTHON_VERSION_MINOR}")
MESSAGE(STATUS "PYTHON_LIBRARIES = ${PYTHON_LIBRARIES}")
MESSAGE(STATUS "PYTHON_EXECUTABLE = ${PYTHON_EXECUTABLE}")
MESSAGE(STATUS "PYTHON_EXECUTABLE_HINT = ${PYTHON_EXECUTABLE_HINT}")
MESSAGE(STATUS "PYTHON_INCLUDE_DIRS = ${PYTHON_INCLUDE_DIRS}")
MESSAGE(STATUS "PYLIB_SUFFIX = ${PYLIB_SUFFIX}")
MESSAGE(STATUS "PYBIND_INCLUDE_DIRS = ${PYBIND_INCLUDE_DIRS}")

IF (${PYTHON_EXECUTABLE_HINT})
    IF (NOT (${PYTHON_EXECUTABLE_HINT} EQUAL ${PYTHON_EXECUTABLE}))
        MESSAGE(FATAL_ERROR "Python3 used by cmake (${PYTHON_EXECUTABLE}) does not match Python3 \
            used by setup.py (${PYTHON_EXECUTABLE_HINT})!")
    ENDIF()
ENDIF()

SET(CMAKE_CXX_STANDARD 11)
SET(CMAKE_CXX_STANDARD_REQUIRED True)

ADD_SUBDIRECTORY(src/newmat10)

FILE(GLOB_RECURSE SRCS src/array/*.C src/block/*.C src/csf/*.C src/dmrg/*.C src/io/*.C
    src/mps_nevpt/*.C src/npdm/*.C src/numeric/*.C src/response/*.C src/symmetry/*.C
    src/four_index_ops/*.C src/three_index_ops/*.C src/genetic/*.C src/two_index_ops/*.C)

FILE(GLOB_RECURSE EXC_SRCS *-checkpoint.C *-checkpoint.cpp)
FILE(GLOB_RECURSE RM_PYBIND_SRCS src/pybind/*.cpp)

LIST(REMOVE_ITEM SRCS ${EXC_SRCS})

MESSAGE(STATUS "BUILD_LIB = ${BUILD_LIB}")

IF (${BOOST_OLD})
    SET(BOOST_FLAG "-DBOOST_NO_CXX11_SCOPED_ENUMS")
ELSE()
    SET(BOOST_FLAG "-DBOOST_1_56_0")
ENDIF() 

IF (${USE_MKL})
    FIND_PATH(MKL_INCLUDE_DIR NAMES mkl.h HINTS $ENV{MKLROOT}/include)
    FIND_LIBRARY(MKL_LIB_GF NAMES mkl_gf_lp64
        PATHS $ENV{MKLROOT}/lib $ENV{MKLROOT}/lib/intel64 NO_DEFAULT_PATH)
    FIND_LIBRARY(MKL_LIB_CORE NAMES mkl_core
        PATHS $ENV{MKLROOT}/lib $ENV{MKLROOT}/lib/intel64 NO_DEFAULT_PATH)
    FIND_LIBRARY(MKL_LIB_SEQ NAMES mkl_sequential
        PATHS $ENV{MKLROOT}/lib $ENV{MKLROOT}/lib/intel64 NO_DEFAULT_PATH)
    FIND_LIBRARY(MKL_LIB_AVX NAMES mkl_avx2
        PATHS $ENV{MKLROOT}/lib $ENV{MKLROOT}/lib/intel64 NO_DEFAULT_PATH)
    SET(MKL_LIBS ${MKL_LIB_GF} ${MKL_LIB_CORE} ${MKL_LIB_SEQ} ${MKL_LIB_AVX})
    MESSAGE(STATUS "MKL_INCLUDE_DIR = ${MKL_INCLUDE_DIR}")
    MESSAGE(STATUS "MKL_LIBS = ${MKL_LIBS}") 
    SET(MKL_FLAG "-D_HAS_INTEL_MKL")
ELSE()
    # Check LAPACK and BLAS
    FIND_PACKAGE(BLAS REQUIRED)
    FIND_PACKAGE(LAPACK REQUIRED)
    
    SET(MKL_INCLUDE_DIR "")
    SET(MKL_LIBS "")
    SET(MKL_FLAG "")
ENDIF()

IF (${BUILD_LIB})
    FILE(GLOB_RECURSE PYBIND_SRCS src/pybind/*.cpp src/rev/*.cpp)
    SET(SRCS ${PYBIND_SRCS} ${SRCS})
    LIST(REMOVE_ITEM SRCS ${EXC_SRCS})

    IF (${NO_PYBIND})
        MESSAGE(STATUS "NO_PYBIND")
        LIST(REMOVE_ITEM SRCS ${RM_PYBIND_SRCS})
        ADD_LIBRARY(${PROJECT_NAME} MODULE ${SRCS})
    ELSE()
        ADD_LIBRARY(${PROJECT_NAME} MODULE ${SRCS})
        SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES SUFFIX ${PYLIB_SUFFIX} PREFIX "")
    ENDIF()

    FIND_PACKAGE(Eigen3 REQUIRED)
    TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC ${EIGEN3_INCLUDE_DIR})
    MESSAGE(STATUS "EIGEN3_INCLUDE_DIR = ${EIGEN3_INCLUDE_DIR}")

ELSE()
    LIST(REMOVE_ITEM SRCS ${RM_PYBIND_SRCS})
    SET(SRCS ${SRCS} src/main.C)
    ADD_EXECUTABLE(${PROJECT_NAME} ${SRCS})
ENDIF()

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC src
    src/array src/block src/csf src/dmrg src/io src/mps_nevpt src/newmat10 src/npdm src/numeric
    src/response src/symmetry src/four_index_ops src/three_index_ops src/two_index_ops src/genetic)

SET(Boost_USE_MULTITHREADED ON)
FIND_PACKAGE(Boost REQUIRED COMPONENTS system filesystem serialization)
TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC ${Boost_INCLUDE_DIRS} ${MKL_INCLUDE_DIR})
TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC newmat pthread rt gomp)
# TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${LAPACK_LIBRARIES} ${BLAS_LIBRARIES} ${MKL_LIBS} ${Boost_LIBRARIES})
TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${LAPACK_LIBRARIES} ${BLAS_LIBRARIES} ${MKL_LIBS} boost_system boost_filesystem boost_serialization)

TARGET_COMPILE_OPTIONS(${PROJECT_NAME} PUBLIC ${BOOST_FLAG} -DBLAS -DUSELAPACK -DFAST_MTP -D_HAS_CBLAS ${MKL_FLAG})

IF (${MPI})
    FIND_PACKAGE(MPI REQUIRED)
    FIND_PACKAGE(Boost REQUIRED COMPONENTS mpi)
    SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES COMPILE_FLAGS "${MPI_COMPILE_FLAGS}")
    SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES LINK_FLAGS "${MPI_LINK_FLAGS}")
    TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC ${MPI_INCLUDE_PATH})
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${MPI_CXX_LIBRARIES})
    MESSAGE(STATUS "MPI_COMPILE_FLAGS = ${MPI_COMPILE_FLAGS}")
    MESSAGE(STATUS "MPI_LINK_FLAGS = ${MPI_LINK_FLAGS}")
    MESSAGE(STATUS "MPI_INCLUDE_PATH = ${MPI_INCLUDE_PATH}")
    MESSAGE(STATUS "MPI_CXX_LIBRARIES = ${MPI_CXX_LIBRARIES}")
ELSE()
    TARGET_COMPILE_OPTIONS(${PROJECT_NAME} PUBLIC -DSERIAL)
ENDIF()

MESSAGE(STATUS "SRCS = ${SRCS}")
MESSAGE(STATUS "Boost_VERSION = ${Boost_VERSION}")
MESSAGE(STATUS "Boost_INCLUDE_DIRS = ${Boost_INCLUDE_DIRS}")

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC ${PYTHON_INCLUDE_DIRS} ${PYBIND_INCLUDE_DIRS})
TARGET_COMPILE_OPTIONS(${PROJECT_NAME} BEFORE PUBLIC -fopenmp -O2 -funroll-loops -Werror -Wno-deprecated-declarations)
